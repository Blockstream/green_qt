#define AppName "Blockstream"
#define PreviousAppName "Blockstream Green"
#define AppVersion "@PROJECT_VERSION@"
#define AppPublisher "Blockstream"
#define AppURL "https://blockstream.com/app/"

[Setup]
AppId={{e6223cfe-f798-4592-a835-c6a9b3d874d8}
AppName={#AppName}
AppVersion={#AppVersion}
AppVerName="Blockstream {#AppVersion}"
AppPublisher={#AppPublisher}
AppPublisherURL={#AppURL}
AppSupportURL={#AppURL}
AppUpdatesURL={#AppURL}
DefaultDirName={pf}\{#AppPublisher}\{#AppName}
UninstallFilesDir={pf}\{#AppPublisher}\{#AppName}
UninstallDisplayIcon={app}\{#AppName}.exe
DefaultGroupName={#AppPublisher}
OutputDir=.
OutputBaseFilename=Setup
Compression=lzma
SolidCompression=yes
ChangesEnvironment=true
PrivilegesRequired=admin
ArchitecturesInstallIn64BitMode=x64
CloseApplications=force
DisableWelcomePage=no
WizardImageFile=wizard.bmp
WizardSmallImageFile=wizard-small.bmp

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Files]
Source: "vc_redist.x64.exe"; DestDir: "{tmp}"; Flags: deleteafterinstall
Source: "*"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs

[Icons]
Name: "{group}\{#AppName}"; Filename: "{app}\{#AppName}.exe"; IconFilename: "{app}\{#AppName}.exe"; IconIndex: 0;
Name: "{group}\{cm:UninstallProgram,{#AppName}}"; Filename: "{uninstallexe}"
Name: "{commondesktop}\{#AppName}"; Filename: "{app}\{#AppName}.exe"; Tasks: desktopicon

[Registry]
Root: HKLM; Subkey: "Software\{#PreviousAppName}"; Flags: deletekey
Root: HKLM; Subkey: "Software\Microsoft\Windows\CurrentVersion\Uninstall\{{3AD17C10-A864-47AB-895F-FA44CA654295}_is1"; Flags: deletekey

[InstallDelete]
Type: files; Name: "{group}\{#PreviousAppName}.lnk"
Type: files; Name: "{group}\Uninstall {#PreviousAppName}.lnk"
Type: files; Name: "{userdesktop}\{#PreviousAppName}.lnk"
Type: files; Name: "{commondesktop}\{#PreviousAppName}.lnk"
Type: filesandordirs; Name: "{pf32}\{#AppPublisher}\{#PreviousAppName}"
Type: filesandordirs; Name: "{pf64}\{#AppPublisher}\{#PreviousAppName}"
Type: filesandordirs; Name: "{pf}\{#AppPublisher}\{#PreviousAppName}"

[Messages]
WelcomeLabel1=Before we start
WelcomeLabel2=To access your existing wallets, please use the new Blockstream app. The installer will replace the old Blockstream Green app, but rest assured, all your wallets are safe and will be fully accessible in the new application.

[Run]
Filename: "{tmp}\vc_redist.x64.exe"; \
    Parameters: "/install /quiet /norestart"; \
    Flags: waituntilterminated; \
    Check: NeedVCRedist64; \
    StatusMsg: "Installing Microsoft Visual C++ 2015â€“2022 Runtime...";

[Code]
const
  WM_CLOSE = 16;

function InitializeSetup : Boolean;
var winHwnd: Longint;
    strProg: string;
begin
  Result := True;
  try
    strProg := 'Blockstream';
    winHwnd := FindWindowByClassName(strProg);
    winHwnd := FindWindowByWindowName(strProg);
    Log('winHwnd: ' + IntToStr(winHwnd));
    if winHwnd <> 0 then
      Result := PostMessage(winHwnd,WM_CLOSE,0,0);
  except
  end;
end;

function InitializeUninstall : Boolean;
var winHwnd: Longint;
    strProg: string;
begin
  Result := True;
  try
    strProg := 'Blockstream';
    winHwnd := FindWindowByClassName(strProg);
    winHwnd := FindWindowByWindowName(strProg);
    Log('winHwnd: ' + IntToStr(winHwnd));
    if winHwnd <> 0 then
      Result := PostMessage(winHwnd,WM_CLOSE,0,0);
  except
  end;
end;

function NeedVCRedist64(): Boolean;
var
  Installed: Cardinal;
begin
  if RegQueryDWordValue(HKLM,
    'SOFTWARE\Microsoft\VisualStudio\14.0\VC\Runtimes\x64',
    'Installed', Installed) then
  begin
    Result := (Installed <> 1);
  end
  else
    Result := True;
end;
